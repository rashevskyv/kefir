# SigHax Uninstaller script (reverts from SigHax to stock)
# This is for retail N3DS units only
# Not recommended on custom NCSDs(!)
# last changed: 20170716
# author: d0k3

set SUCCESSMSG "CFW uninstalled successfully"

# check sector 0x96
set ERRORMSG "Sector 0x96 is not genuine.\nCan't continue."
sha S:/sector0x96.bin 82F2730D2C2DA3F30165F987FDCCAC5CBAB24B4E5F65C981CD7BE6F438E6D9D3

# grab NATIVE_FIRM out of the NCCH
set ERRORMSG "NATIVE_FIRM not found.\nIs this a N3DS?"
find -f 1:/title/00040138/20000002/content/????????.app NATIVE_NCCH
imgmount $[NATIVE_NCCH]
verify G:/exefs/.firm
set NATIVE_FIRM $[GM9OUT]/NATIVE_FIRM.firm
cp -w G:/exefs/.firm $[NATIVE_FIRM]
imgumount

# Write NATIVE_FIRM to the FIRM partitions
set ERRORMSG "Sighax not uninstalled.\nNo changes written to the system."
ask "!!WARNING!!\nThis will remove sighax and\nrevert your system to stock.\n \nProceed?"
allow S:/firm0.bin # same permission level for all firms
allow 1:/ # for ctrnand file removal
rm -o -s 1:/boot.firm
rm -o -s 1:/rw/luma
cp -n $[NATIVE_FIRM] S:/firm0.bin
cp -n $[NATIVE_FIRM] S:/firm1.bin
