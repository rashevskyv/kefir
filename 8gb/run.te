#REQUIRE SD

p = println
bkp_base = "sd:/config/uberhand/4gb_config_backup"
update_source = "sd:/8gb"

files_to_backup = [
    "atmosphere/package3",
    "atmosphere/reboot_payload.bin",
    "bootloader/bootlogo_kefir.bmp",
    "bootloader/payloads/fusee.bin",
    "bootloader/sys/libsys_lp0.bso",
    "bootloader/sys/libsys_minerva.bso",
    "bootloader/sys/module_sample.bso",
    "bootloader/sys/nyx.bin",
    "bootloader/update.bin",
    "payload.bin"
]

if (fsexists(update_source)) {
    
    p("Creating backup...")
    
    if (fsexists(bkp_base)) { deldir(bkp_base) }
    
    mkdir("sd:/config")
    mkdir("sd:/config/uberhand")
    mkdir(bkp_base)
    
    mkdir(bkp_base + "/atmosphere")
    mkdir(bkp_base + "/bootloader")
    mkdir(bkp_base + "/bootloader/payloads")
    mkdir(bkp_base + "/bootloader/sys")

    files_to_backup.foreach("f") {
        src = "sd:/" + f
        dst = bkp_base + "/" + f
        
        if (fsexists(src)) {
            copyfile(src, dst)
        }
    }

    mkdir("sd:/tegraexplorer")
    mkdir("sd:/tegraexplorer/scripts")
    copyfile(update_source + "/remove.te", "sd:/tegraexplorer/scripts/Remove_8GB-RAM_config.te")

    p("Installing update...")
    
    copydir(update_source + "/atmosphere", "sd:/")
    copydir(update_source + "/bootloader", "sd:/")
    copyfile(update_source + "/payload.bin", "sd:/payload.bin")

    p("Cleaning up...")
    
    deldir(update_source)
    delfile("sd:/startup.te")
    
    if (fsexists("sd:/4gb_config_backup")) {
        deldir("sd:/4gb_config_backup")
    }

    p("Done. Rebooting.")
    
    if (is_erista()) {
        payload("sd:/payload.bin")
    }.else(){
        reboot_ofw()
    }
}